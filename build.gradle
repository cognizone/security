plugins {
  id 'pl.allegro.tech.build.axion-release' version '1.13.3'
  id 'org.springframework.boot' version '2.6.14' apply false
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'idea'
  id 'java'
  id 'java-library'
  id 'maven-publish'
  id "io.freefair.lombok" version "6.5.1"
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://repo.spring.io/milestone" }
}

scmVersion {
  tag {
    prefix = "v"
    versionSeparator = ""
    branchPrefix = [
            'release/.*': 'release-v',
            'hotfix/.*' : 'hotfix-v'
    ]
  }
  nextVersion {
    suffix = "SNAPSHOT"
    separator = "-"
  }
}

ext {
  assertJVersion = "3.24.2"
  asquareVersion = "0.6.18"
  commonsCollectionsVersion = "4.4"
  commonsLang3Version = "3.12.0"
  junitVersion = "5.9.2"
  snakeyamlVersion = "1.33"
}

idea {
  project {
    jdkName = "11"
    languageLevel = "11"
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: "io.freefair.lombok"
  apply plugin: 'io.spring.dependency-management'
  apply plugin: "maven-publish"
  apply plugin: 'signing'

  group = 'zone.cogni.lib.security'
  version = scmVersion.version
  sourceCompatibility = '11'

  java {
    withJavadocJar()
    withSourcesJar()
  }

  repositories {
    if (project.hasProperty('jenkins-ci')) {
      maven {
        url "${System.properties['nexus.url']}/repository/cognizone-group"
        credentials {
          username = System.properties['nexus.username']
          password = System.properties['nexus.password']
        }
        allowInsecureProtocol(true)
      }
    }
    mavenLocal()
    mavenCentral()
    maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://repo.spring.io/milestone" }
  }

  dependencyManagement {
    imports {
      mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
  }


  tasks.named('test') {
    useJUnitPlatform()
  }
}

project("security-empty-permission") {
}

project("security") {
  apply plugin: "maven-publish"

  dependencies {
    compileOnly project(':security-empty-permission')
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    implementation("zone.cogni.asquare:cogni-core:${asquareVersion}")
    implementation("org.yaml:snakeyaml:${snakeyamlVersion}") //because asquare says: Provides transitive vulnerable dependency org.yaml:snakeyaml:1.29

    // libs
    implementation("org.apache.commons:commons-collections4:${commonsCollectionsVersion}")
    implementation("org.apache.commons:commons-lang3:${commonsLang3Version}")
    implementation("javax.inject:javax.inject:1")

    // spring
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.security:spring-security-saml2-service-provider')

    //test
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.springframework.security:spring-security-test")
    testImplementation("org.junit.jupiter:junit-jupiter:${junitVersion}")
    testImplementation("org.assertj:assertj-core:${assertJVersion}")
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        groupId = group
        artifactId = 'security'
        version = scmVersion.version
        from components.java

        pom {
          name = "${project.name}"
          packaging = 'jar'
          // optionally artifactId can be defined here
          description = 'Cognizone Security'
          url = '<https://github.com/cognizone/security>'

          scm {
            connection = 'scm:git:git@<https://github.com/cognizone/security.git>'
            developerConnection = 'scm:git:git@github.com:cognizone/security.git'
            url = '<https://github.com/cognizone/security>'
          }

          licenses {
            license {
              name = 'The Apache License, Version 2.0'
              url = '<http://www.apache.org/licenses/LICENSE-2.0.txt>'
            }
          }

          developers {
            developer {
              id = 'cognizone'
              name = 'Cognizone'
              email = 'cognizone-lib-dev@cogni.zone'
            }
          }
        }
      }
    }

    repositories {
      if (project.hasProperty("publishToCognizoneArchiva")) {
        throw new GradleException("Publish to cognizone archiva is deprecated")
      }
      if (project.hasProperty("publishToCognizoneNexus")) {
        maven {
          credentials {
            username = System.properties['nexus.username']
            password = System.properties['nexus.password']
          }
          def releasesRepoUrl = "${System.properties['nexus.url']}/repository/cognizone-release"
          def snapshotsRepoUrl = "${System.properties['nexus.url']}/repository/cognizone-snapshot"
          url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
          allowInsecureProtocol(true)
        }
      }
      if (project.hasProperty("publishToMavenCentral")) {
        maven {
          credentials {
            username = System.properties['ossrh.username']
            password = System.properties['ossrh.password']
          }
          // Define repository URLs based on the version
          def stagingRepoUrl = "${System.properties['ossrh.url']}/service/local/staging/deploy/maven2"
          def snapshotsRepoUrl = "${System.properties['ossrh.url']}/content/repositories/snapshots"
          url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : stagingRepoUrl
        }
      }
    }
  }
  signing {
    if (project.hasProperty("publishToMavenCentral")) {
      sign publishing.publications.mavenJava
    }
  }

  tasks.withType(Javadoc) {
    failOnError false
  }
}

